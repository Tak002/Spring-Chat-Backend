plugins {
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
}

allprojects {
    group = 'com.example'
    version = '0.1.0'
    repositories { mavenCentral() }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain { languageVersion = JavaLanguageVersion.of(21) }
    }
    configurations {
        compileOnly { extendsFrom annotationProcessor }
        testCompileOnly { extendsFrom testAnnotationProcessor }
    }
    dependencyManagement { //버전 없이 dependencies 사용 가능
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.4"
        }
    }
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    }

    // ✅ Spring에서 파라미터 이름 필요할 때 오류 방지(-parameters)
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-parameters']
    }
    test { useJUnitPlatform() }
}
